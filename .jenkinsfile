// ### Environment ###
properties([
	parameters([
		string(name:"SBA_VERSION", defaultValue: "", description: "version of sba", trim: true),
		string(name:"SBA_NODE",    defaultValue: "${env.SBA_NODE}", description: "execution node name", trim: true)
	]),
	disableConcurrentBuilds()
])

// Get mail parameters from jenkins env vars
url           = "${env.NPM_SERVER_URL}"
npm_user      = "${env.NPM_USER}"
npm_pass      = "${env.NPM_PASS}"
npm_mail      = "${env.NPM_MAIL}"
// default version number of the develop branch
developNumber = "${env.NPM_DEVELOP_NUMBER}"

// get job variables from job parameters
sba_version   = "${params.SBA_VERSION}"

// .npmrc variables for verdaccio connection
scope  = "@sinequa"
fnpmrc = '.npmrc'
anpmrc = ["always-auth=true", "registry=${url}/", "scope=${scope}"]


// Pipeline workflow (main)
node (params.SBA_NODE) {

	currentBuild.result = "SUCCESS"
	try {

		// get the source code
		checkout scm
		
		// load jenkins functions
		def rootDir = pwd()
		def jf = load "${rootDir}/.jenkins_function.groovy"
		def currentBranch = jf.findBranchNumber()
		jf.set_sba_version(currentBranch)

		stage('Clean') {
			// delete dependency modules
			def nmfolder = "node_modules"
			echo ".Rmdir /s /q ${nmfolder}"
			bat "if exist ${nmfolder} ( rmdir /s /q ${nmfolder} )"
			
			// clean the npm cache
			echo ".Cache clean force"
			bat "npm cache clean --force"
		}
		
		stage('Create user') {
			// create user for verdaccio
			bat "npm-cli-login -u ${npm_user} -p ${npm_pass} -e ${npm_mail} -r ${url} -s ${scope} --config-path ${fnpmrc}"
			// add connection infos in .npmrc file
			jf.appendFile(fnpmrc, anpmrc)
			// check the connection
			bat "npm whoami"
		}

		stage('NPM Install') {
			// get the dependency modules
			bat "npm i"
		}
		
		// check if if we are in standard case of build or merge (case of PR)
		def action = jf.buildOrMerge()
		if ("${action}" == "build") {
			// Build the app started on commit event
			stage('Build') {
				echo "npm run build --if-present"
				bat "npm run build --if-present"
			}
			
		} else {
		
			// validate the build before merge
			// app are fired on merge event
			stage('Validation') {
				echo "npm run buildsq --sq_version=${sba_version} --if-present"
				bat "npm run buildsq --sq_version=${sba_version} --if-present"
			}
		}

	} catch (err) {
		currentBuild.result = "FAILURE"
		throw err
	}
}
